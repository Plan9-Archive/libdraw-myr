use std
use bio

use "types"
use "image"
use "load"

pkg draw =
	const openfont	: (dpy : display#, name : byte[:] -> std.result(font#, err))

	pkglocal const loadsubfont	: (dpy : display#, sub : subfont# -> void)
	pkglocal const cachechar	: (font : font#, c : char -> int16)
;;

const openfont = {dpy, name
	var dir

	dir = std.dirname(name)
	match bio.open(name, bio.Rd)
	| `std.Ok f:	-> loadfont(dpy, dir, f)
	| `std.Err e:	-> `std.Err `Efont
	;;
}

const cachechar = {ft, c
	-> -1
}

const loadfont = {dpy, dir, f
	var sp : byte[:][3]
	var lo, hi, start, n, r
	var font

	font = std.zalloc()
	match bio.readln(f)
	| `bio.Ok ln:
		std.bstrtok(sp[:2], ln)
		font.height = std.getv(std.intparse(sp[0]), -1)
		font.ascent = std.getv(std.intparse(sp[0]), -1)
	| `bio.Eof:
	| `bio.Err e:
	;;

	for ln in bio.byline(f)
		n = 0
		start = 0
		std.bstrtok(sp[:3], ln)
		lo = std.getv(std.intparse(sp[n++]), -1)
		hi = std.getv(std.intparse(sp[n++]), -1)
		if sp.len == 4
			start = std.getv(std.intparse(sp[n++]), -1)
		;;
		std.slpush(&font.subf, [
			.lo=lo,
			.hi=hi,
			.start=start,
			.path=std.pathcat(dir, sp[n]),
			.image=`std.None,
			.glyph=`std.None,
		])
	;;

	/* heuristic: probably big enough... */
	r = [.x0 = 0, .y0 = 0, .x1 = 1000, .y1 = 10*font.ascent]
	font.cache = genallocimage(dpy, 0, DTransparent, GREY8, DBlack, false, r, r)
	-> `std.Ok font
}

const loadsubfont = {dpy, font
	var buf

	match std.slurp(font.path)
	| `std.Err e:	std.fatal("could not open font {}: {}\n", font.path, e)
	| `std.Ok b:	buf = b
	;;

	match xload(dpy, buf)
	| `std.Err e:	std.fatal("could not load image {}: {}\n")
	| `std.Ok (img, off):
		match readglyphs(buf[off:])
		| `std.Ok gd:
			font.image = `std.Some img
			font.glyph = `std.Some gd
		| `std.Err e:
			std.fatal("invalid glyph data in font {}\n", font.path)
		;;
	;;
}

const readglyphs = {glyphdata
	var sp : byte[:][3]
	var n, height, ascent, err
	var gd, g

	if glyphdata.len < 3*12 || std.bstrtok(sp[:], glyphdata[:3*12]).len != 3
		-> `std.Err `Efmt
	;;

	err = false
	n = getint(sp[0], &err)
	height = getint(sp[1], &err)
	ascent = getint(sp[2], &err)
	glyphdata = glyphdata[3*12:]
	if err || glyphdata.len < n
		-> `std.Err `Efmt
	;;

	gd = [][:]
	for var i = 0; i <= n; i++
		g = [
			.x=std.getle16(glyphdata[6*i+0 : 6*i+2]),
			.top=(glyphdata[6*i + 2] : int),
			.bottom=(glyphdata[6*i + 3] : int),
			.width=(glyphdata[6*i + 4] : int),
			.height=(glyphdata[6*i + 5] : int),
		]
		std.slpush(&gd, g)
	;;
	-> `std.Ok gd
}
